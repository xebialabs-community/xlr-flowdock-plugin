plugins {
  id "com.github.hierynomus.license" version "0.14.0"
  id "com.xebialabs.xl.docker" version "1.1.0"
  id 'nebula.release' version '6.0.0'
  id "com.github.hierynomus.jython" version "0.8.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

xlDocker {
  compileImage = 'xebialabs/xlr_dev_compile'
  compileVersion = 'v7.2.0.2'
  runImage = 'xebialabs/xlr_dev_run'
  runVersion = 'v7.2.0.1'
  runPortMapping = '15516:5516'
}

def apiVersion = '2018.1.0'
def xlrVersion = '8.0.0'

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}


repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://dist.xebialabs.com/public/maven2'
  }
}

dependencies {
  compile group: "com.xebialabs.xlrelease", name: "xlr-api", version: xlrVersion, transitive: false
  compile "com.xebialabs.deployit:udm-plugin-api:$apiVersion"
  jython ":requests:2.11.1"
  jython(":python-flowdock:0.1") {
    artifact {
      name = "flowdock"
      extension = "py"
    }
  }
}

license {
  header rootProject.file('License.md')
  strictCheck false
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
  excludes(["**/requests/**/*.py", "**/flowdock.py"])
}
